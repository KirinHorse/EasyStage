buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.netflix.nebula:nebula-project-plugin:3.3.0'
    }
}

apply plugin: "idea"
apply plugin: "java"
apply plugin: 'nebula.facet'

version = '1.0'
ext {
    appName = "EasyStage"
    gdxVersion = System.env.GDX_VERSION != null ? System.env.GDX_VERSION : '1.9.5'
    cglibVersion = '3.2.4'
    nativefontVersion = '2.4.1'
}

repositories {
    jcenter()
    maven { url "https://jitpack.io/" }
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "cglib:cglib:$cglibVersion"
    compile "com.github.tianqiujie:nativefont:$nativefontVersion"
}

sourceCompatibility = JavaVersion.VERSION_1_7
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

facets {
    demo
}
task run(dependsOn: demoClasses, type: JavaExec) {
    main = "com.ayocrazy.easystage.DesktopLauncher"
    classpath = sourceSets.demo.runtimeClasspath
    standardInput = System.in
    workingDir = "$projectDir/src/demo/resources"
    ignoreExitValue = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

idea {
    module {
        [".gradle",".idea","build"].each {
            excludeDirs += file("$it")
        }
    }
}

